#!/usr/bin/env perl

use strict;
use warnings;
use utf8;
use feature 'say';

use Time::Piece;
use File::Basename 'basename';
use File::Path 'mkpath';
use Net::Twitter::Lite::WithAPIv1_1;
use YAML::Tiny;
use Furl;

my $settings = YAML::Tiny->read('./settings.yml')->[0];
my $http = Furl->new();

# Autoflush
$|=1;

# Authentication
my $nt = Net::Twitter::Lite::WithAPIv1_1->new(
  consumer_key        => $settings->{consumer_key},
  consumer_secret     => $settings->{consumer_secret},
  access_token        => $settings->{access_token},
  access_token_secret => $settings->{access_token_secret}
);

# cd
if (!-d $settings->{outdir}) { mkdir $settings->{outdir} or die; }
chdir $settings->{outdir} or die;
my $saved_files = [map {basename($_)} (split /\n/, `find .`)];

my $mediators = $settings->{mediators};
my $all_statuses = [];

{
  my $max_id;
  for my $iter (1..4) {
    my $timeline;
    $timeline = $nt->home_timeline({count => 200}) if !defined($max_id);
    $timeline = $nt->home_timeline({count => 200, max_id => $max_id}) if defined($max_id);
    push(@$all_statuses, @$timeline);
    $max_id = $timeline->[-1]{id};
  }
}

for my $mediator (@$mediators) {
  my $max_id;
  for my $iter (1..4) {
    my $favorites;
    $favorites = $nt->favorites({screen_name => $mediator, count => 200}) if !defined($max_id);
    $favorites = $nt->favorites({screen_name => $mediator, count => 200, max_id => $max_id}) if defined($max_id);
    push(@$all_statuses, @$favorites);
    $max_id = $favorites->[-1]{id};
  }
}

my %tmp;
my $unique_statuses = [grep {!$tmp{$_->{id}}++} @$all_statuses];
my $sorted_statuses = 
  [sort {
    Time::Piece->strptime($a->{created_at}, '%a %b %d %T %z %Y')
    <=>
    Time::Piece->strptime($b->{created_at}, '%a %b %d %T %z %Y')
  } @$unique_statuses];

for my $status (@$sorted_statuses) {
  my $media_array = $status->{extended_entities}{media};
  download($media_array) if $media_array;
}

sub download {
  my $media_array = shift;
  my $binary;

  if($media_array->[0]{video_info}) {
    my $video = $media_array->[0]{video_info}{variants};
    for (@$video) { $_->{bitrate} = 0 unless $_->{bitrate} }
    my $url = (sort { $b->{bitrate} <=> $a->{bitrate} } @$video)[0]{url};
    $url =~ s/\?.+//;

    my $filename = basename($url);
    if (grep {$filename eq $_} @$saved_files or -f "./$filename") {
      say "[@{[ localtime->datetime ]}]Already saved     : $filename";
      return;
    }

    $binary = $http->get($url);
    die "[@{[ localtime->datetime ]}]Cannot fetch video: returned " . $binary->code . ", url: $url"
      if grep {$_ eq $binary->code} (404, 500);
    save($filename, $binary);
  } else {
    for my $image (@$media_array) {
      my $url = $image->{media_url};

      my $filename = basename($url);
      if (grep {$filename eq $_} @$saved_files or -f "./$filename") {
        say "[@{[ localtime->datetime ]}]Already saved     : $filename";
        return;
      }

      $binary = $http->get($url.'?name=orig');
      die "[@{[ localtime->datetime ]}]Cannot fetch image: returned " . $binary->code . ", url: $url"
        if grep {$_ eq $binary->code} (404, 500);
      save($filename, $binary);
    }
  }
}

sub save {
  my ($filename, $binary) = @_;

  open my $fh, ">", $filename
    or die "[@{[ localtime->datetime ]}]Cannot create file: $!, filename: ".$filename;
  say $fh $binary->content;
  close $fh;
  say "[@{[ localtime->datetime ]}]Image saved       : $filename";
}
